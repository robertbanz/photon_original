
	; DATE	12-16-1986
	; EDITED BY ERJ


;4-2-87 DSe
;	(1) TG21: caused reset of rf uart RDA to strobe data into 810
;	(2) clear display upon next trigger pull
;		TG21: clear dsply of previous score
;	    	TGWAIT: do not clear dsply of score
;	(3) CODETB: replaced 80H eith 94H


CONT	EQU	1000H		; I/O CONTROL LATCH
RAM1	EQU	2000H		;
RAM2	EQU	4000H
STATUS	EQU	8000H		; BIT0-/BF BIT1-TRIG BIT2-TEOC
				; BIT3-RFRDA BIT4-IRRDA
STK	EQU	207FH

	;---- RAM LOCATIONS ----

IO	EQU	RAM1
MCRED	EQU	IO+1
TTIME	EQU	IO+2
VALUE	EQU	IO+4
MUNIT	EQU	IO+5
MTEN	EQU	IO+6
MHUND	EQU	IO+7
MTHOU	EQU	IO+8
GAMCNT	EQU	IO+9
COLOR	EQU	IO+0BH
READY	EQU	IO+0CH
CRED_FLG EQU	IO+0DH
CSTAT	EQU	1
CLEAR	EQU	0
UART	EQU	0
PTA	EQU	20H		; IR RECVD DATA
PTB	EQU	21H
PTC	EQU	22H
DDRA	EQU	24H
DDRB	EQU	25H
DDRC	EQU	26H
MDR	EQU	27H
T0LSB	EQU	30H
T0MSB	EQU	31H
T1LSB	EQU	32H
T1MSB	EQU	33H
STPT0	EQU	34H
STRT0	EQU	35H
STPT1	EQU	36H
STRT1	EQU	37H
T0MD	EQU	38H
T1MD	EQU	39H
PTAS	EQU	40H
PTBS	EQU	41H
PTCS	EQU	42H
DDRAS	EQU	44H
DDRBS	EQU	45H
DDRCS	EQU	46H
MDRS	EQU	47H
T0LSBS	EQU	50H
T0MSBS	EQU	51H
T1LSBS	EQU	52H
T1MSBS	EQU	53H
STPT0S	EQU	54H
STRT0S	EQU	55H
STPT1S	EQU	56H
STRT1S	EQU	57H
T0MDS	EQU	58H
T1MDS	EQU	59H
GSYNC	EQU	0CAH
CREDIT	EQU	0C5H

	;---- DISPLAYS -----

THOUS	EQU	1
HUNDS	EQU	2
TENS	EQU	3
UNITS	EQU	4
VAL10	EQU	5
VAL1	EQU	6
CRED	EQU	7
RED	EQU	8
GRN	EQU	9
RED50	EQU	44H
GRN50	EQU	82H
OMRED50	EQU	45H
OMGRN50	EQU	83H
	EJEC

;----------------------------------------
;	POWER-UP COLDSTART
;----------------------------------------

	ASEG
	ORG	0

COLD:
	JP	BEGIN

;----------------------------------------
;	RST C	TIMER 1
;----------------------------------------

	ORG	002CH
RSTC:
	JP	TIMER1

;----------------------------------------
;	RST B	INTERRUPT NOT USED
;----------------------------------------

	ORG	34H
RSTB:
	JP	COIN

;----------------------------------------
;	RST A	TIMER 0
;----------------------------------------

	ORG	3CH
RSTA:
	JP	TIMER0
	EJEC

;----------------------------------------
;	SYSTEM INITIALIZATION
;----------------------------------------

	ORG	100H
;	POWER-UP INIT

BEGIN:
	XOR	A
	OUT	(CRED),A	; CLEAR CREDITS
	LD	(MCRED),A	; CLEAR SCORE DISPLAYS
	LD	(MUNIT),A
	LD	(MTEN),A
	LD	(MHUND),A
	LD	(MTHOU),A
	LD	(CRED_FLG),A
	LD	SP,STK		; SET STACK POINTER
	LD	A,0EH
	OUT	(0BBH),A	; INTERRUPTS
	CALL	DISP
	DI

		;	INIT 810'S
	XOR	A
	OUT	(MDRS),A
	OUT	(DDRA),A
	OUT	(T0MD),A
	OUT	(T1MD),A
	OUT	(T0MDS),A
	OUT	(T1MDS),A
	INC	A
	OUT	(MDR),A
	LD	A,6
	OUT	(T0MD),A
	OUT	(T1MD),A
	OUT	(T0MDS),A
	OUT	(T1MDS),A
	RRCA
	OUT	(DDRBS),A
	LD	A,23H
	OUT	(DDRC),A
	LD	A,20H
	OUT	(DDRCS),A
	LD	A,0FFH
	OUT	(DDRB),A
	LD	A,0FFH
	OUT	(DDRAS),A

		; SET UP TIMER 0 AND TIMER1 DIVISORS

	LD	A,0FBH
	OUT	(T0LSB),A
	LD	A,8
	OUT	(T0MSB),A
	OUT	(STRT0),A
	LD	A,0F9H
	OUT	(T1LSB),A
	LD	A,04AH
	OUT	(T1MSB),A
	CALL	IMHIT

;----------------------------------------
;	POST GAME WARM START
;----------------------------------------

START:
		; ZERO RAM

	LD	HL,RAM1+2
	LD	B,07EH
INIT1:
	LD	(HL),0
	INC	HL
	DJNZ	INIT1
	LD	HL,RAM2
	LD	B,080H
INIT2:
	LD	(HL),0
	INC	HL
	DJNZ	INIT2

	LD	SP,STK

		; INIT TRI-COLOR LEDS

	LD	A,55H
	LD	(VALUE),A
	OUT	(RED),A
	CPL
	OUT	(GRN),A	
	OUT	(STRT0),A
	EI

		; INIT TARGET VALUE DISPLAYS

	XOR	A
	OUT	(VAL10),A
	OUT	(VAL1),A

		; INIT CONTROL LATCH

	LD	A,33H
	LD	(CONT),A
	LD	(IO),A

;---- CHECK FOR 3 GSYNCS

	LD	B,0
STAT:
	LD	A,(STATUS)
	AND	4
	JP	Z,STAT
	IN	A,(UART)
	CALL	RSUART
	CP	GSYNC
	JR	Z,GOT_GSYNC
	CP	CREDIT
	CALL	Z,COINS
	JR	STAT
GOT_GSYNC:
	INC	B
	LD	A,3
	CP	B
	JP	NZ,STAT

;---- START GAME TIMER

	OUT	(STRT1),A

;---- CHECK FOR CREDITS DURING 30 SEC TIMEOUT

SEC:
	LD	A,(READY)
	CP	55H
	JP	Z,WAIT
	LD	A,(STATUS)	; CHECK FOR CREDIT FROM GAMEP
	AND	4
	JP	Z,CK_CRED	; IF NO DATA, SKIP
	IN	A,(UART)
	CALL	RSUART
	CP	CREDIT
	CALL	Z,COINS
CK_CRED:
	LD	A,(MCRED)
	CP	0
	JP	Z,SEC
	 
	LD	A,(MCRED)
	DEC	A
	LD	(MCRED),A
	OUT	(CRED),A

;---- CLEAR PREVIOUS SCORE

	CALL	DISP
 
		; STOP LED TIMER

	OUT	(STPT0),A


TRIG:
		; AWAIT TRIGGER PULL OR CREDIT FROM GAMEP
	LD	A,(STATUS)
	BIT	1,A
	JR	NZ,GOT_TRIG
	AND	4
	JR	Z,TRIG
	IN	A,(UART)
	CALL	RSUART
	CP	CREDIT
	CALL	Z,COINS
	JR	TRIG

		; START TRIGGER PULL DURATION TIMER

GOT_TRIG:
	LD	HL,875
	LD	(TTIME),HL

TG2:
		; INIT FOR THIS NEW TRIGGER PULL

	CALL	IRRST
	IN	A,(PTA)		; CLEAR ANY OLD IR DATA IN 810

TG21:		; AWAIT TRIGGER PULL TIMEOUT OR IR DATA

	CALL	TIMEOUT
	JP	C,MEXIT		; TIMED OUT

	LD	A,(STATUS)
	BIT	4,A
	JR	Z,TG21		; NO IR READ

		; READ IR AND VALIDATE

	XOR	A		;4-2-87 clear dsplayd previous score
	OUT	(VAL10),A

	CALL	IRRST		;4-2-87  reset RDA flag on uart to cause
				;uart data to be strobed into 810
	IN	A,(PTA)
	LD	HL,CODETBL
	CALL	SEARCH
	JR	C,MEXIT		; IR DATA INVALID

		; PROCESS VALID IR CODE

	LD	B,A
	CALL	GOODHT
	LD	A,(COLOR)	; HAS A STATION COLOR BEEN ASSIGNED
	CP	0
	CALL	Z,SETLED	; NO, SET STATION COLOR
	LD	A,B
	CP	RED50		; CHECK FOR BONUS PLAYERS
	JP	Z,SCOR50
	CP	GRN50
	JP	Z,SCOR50
	CP	OMRED50		; CHECK OMEGA PLAYERS TOO
	JP	Z,SCOR50
	CP	OMGRN50
	JP	Z,SCOR50
	AND	0C0H
	LD	B,A
	LD	A,(COLOR)
	CP	B		; AM I THE SAME COLOR AS THE IR DATA
	JP	NZ,SCOR20	; NO, SCORE 20 POINTS
	JP	SCOR10		; YES, SCORE 10 POINTS

	XOR	A
	LD	B,A
	LD	C,B

GRET:		; INIT FOR NEXT TRIGGER PULL

	CALL	TGWAIT
	JP	TRIG

		; MISS EXIT

MEXIT:
	CALL	MISS
	XOR	A
	OUT	(VAL10),A
	CALL 	TGWAIT
	JP	TRIG

		; SET STATION COLOR

SETLED:
	LD	A,B
	AND	0C0H
	CP	80H
	JP	Z,SETGRN
SETRED:
	LD	(COLOR),A
	LD	A,0FFH
	OUT	(GRN),A
	XOR	A
	OUT	(RED),A
	RET

SETGRN:
	LD	(COLOR),A
	LD	A,0FFH
	OUT	(RED),A
	XOR	A
	OUT	(GRN),A
	RET

		; TRIGGER DURATION TIMER
		; MAINTAINS SOFTWARE COUNTER
		;  EXIT:
		; IF TIMES OUT, SETS CF, RETURNS
		; ELSE, RETURNS WITH CF CLEARED

TIMEOUT:
	LD	HL,(TTIME)
	DEC	HL
	LD	(TTIME),HL
	LD	A,H
	OR	L
	SCF
	RET	Z
	OR	A		; TIMED OUT, CLEAR CF
	RET

		; AWAIT TRIGGER RELEASE

TGWAIT:
	CALL	DEL1K		; DELAY 1.1 SEC

	;4-2-87 remove clear dsplyd score, see TG21
;	XOR	A
;	OUT	(VAL10),A

TGW1:
	LD	A,(STATUS)
	BIT	1,A
	JR	NZ,TGW1
	RET

WAIT:
	JP	WAIT

		; ADD 10 POINTS TO SCORE

SCOR10:
	LD	A,1
	OUT	(VAL10),A
	CALL	SCORE
	JP	GRET

		; ADD 20 POINTS TO SCORE

SCOR20:
	LD	A,2
	OUT	(VAL10),A
NEXT1:
	CALL	SCORE
	CALL	SCORE
	JP	GRET

		; ADD 50 POINTS TO SCORE

SCOR50:
	LD	A,5
	OUT	(VAL10),A
	CALL	SCORE
	CALL	SCORE
	CALL	SCORE
	JP	NEXT1

;-------------------------------
;            COIN
;-------------------------------

COIN:   PUSH    AF
        PUSH    BC
        PUSH    DE
        PUSH    HL
        IN      A,(CSTAT)
        LD      C,A
        LD      B,0FFH
SAMPLE: IN      A,(CSTAT)
        CP      C
        JP      NZ,CNRET
        LD      C,A
        DJNZ    SAMPLE
 
        IN      A,(CSTAT)                         
        AND     3         
        CP      3
        JP      Z,TESTSW                                    
        BIT     0,A
        CALL    Z,COINS
        BIT     1,A
        CALL    Z,COINS
RELESE: IN      A,(CSTAT)
        AND     3
        CP      3
        JP      NZ,RELESE
CNRET:  POP     HL
        POP     DE
        POP     BC
        POP     AF
        EI
        RET
 
TESTSW: IN      A,(CSTAT)
        AND     4
        JP      Z,TEST  
        JP      CNRET


COINS:
	PUSH	AF
	LD	A,(MCRED)	; HAVE THE NUMBER OF CREDITS = 9
	CP	9
	JP	Z,EXIT		; YES, CANT GO ANY HIGHER SO EXIT
	INC	A		; NO, INCREMENT NUMBER OF CREDITS
	LD	(MCRED),A
	OUT	(CRED),A
	CALL	IMHIT
EXIT:
	POP	AF
	RET


TIMER0:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,(VALUE)
	OUT	(RED),A
	CPL
	OUT	(GRN),A
	LD	(VALUE),A
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	EI
	RET


;--------------------------------
;            TIMER 1
;--------------------------------

TIMER1: PUSH    AF
        PUSH    BC
        PUSH    DE
        PUSH    HL
        LD      DE,(GAMCNT)  
        INC     DE
        LD      (GAMCNT),DE
        LD      A,D
        CP      1                                
        JP      NZ,T1RET
        LD      A,E
        CP      070H
        JP      NZ,T1RET
        OUT     (STPT1),A
        DI
        JP      START
 
T1RET:  LD      A,D
        CP      0
        JP      NZ,T1RETT
        LD      A,E
        CP      30  
        JP      NZ,T1RETT
        LD      A,055H
        LD      (READY),A
T1RETT: POP     HL
        POP     DE
        POP     BC
        POP     AF
        EI
        RET


		; RESET UART

RSUART:
	PUSH	AF
	LD	A,(IO)
	OR	8
	LD	(CONT),A
	LD	A,(IO)
	LD	(CONT),A
	POP	AF
	RET

		; SCORING ROUTINE

SCORE:
	LD	A,(MTEN)
	CP	9
	JP	Z,INCHUN
	INC	A
	LD	(MTEN),A
	JP	DISP
INCHUN:
	XOR	A
	LD	(MTEN),A
	LD	A,(MHUND)
	CP	4
	JR	Z,CRED_INC
	CP	9
	JP	Z,INCTHOU
	INC	A
	LD	(MHUND),A
	JP	DISP
CRED_INC:
	INC	A
	LD	(MHUND),A
	LD	A,(CRED_FLG)	; SEE IF CREDIT FLAG SET
	OR	A
	JP	Z,DISP		; IF NOT DONT INC CREDIT
	LD	A,(MCRED)	; GET CURRENT CREDITS
	CP	9		; MAX CREDIT
	JP	Z,DISP		; CANT EXCEED MAX
	INC	A		; ADD ONE CREDIT
	LD	(MCRED),A	; SAVE NEW COUNT
	OUT	(CRED),A	; DISPLAY NEW COUNT
	XOR	A
	LD	(CRED_FLG),A
	JP	DISP
INCTHOU:
	XOR	A
	LD	(MHUND),A
	LD	A,(MTHOU)
	CP	2
	JR	Z,SET_FLG
	CP	9
	JP	Z,INCRED
	INC	A
	LD	(MTHOU),A
	JP	DISP
SET_FLG:
	INC	A
	LD	(MTHOU),A
	LD	(CRED_FLG),A
	JP	DISP
INCRED:
	XOR	A
	LD	(MTHOU),A
	JP	DISP
DISP:
	PUSH	AF
	LD	A,(MUNIT)
	OUT	(UNITS),A
	LD	A,(MTEN)
	OUT	(TENS),A
	LD	A,(MHUND)
	OUT	(HUNDS),A
	LD	A,(MTHOU)
	OUT	(THOUS),A
	POP	AF
	RET

		; TEST ROUTINE

TEST:
	XOR	A
TEST1:
	CALL	TDISP		; CYCLE DISPLAYS THROUGH ALL #'S
	CALL	DELAAY
	INC	A
	CP	010H
	JP	NZ,TEST1
	XOR	A
	CALL	TDISP
	OUT	(RED),A		; TURN RED AND GREEN OUT
	OUT	(GRN),A
	CALL	DELAAY
	LD	A,0FFH
	OUT	(RED),A		; TURN RED ON
	CALL	DELAAY
	XOR	A
	OUT	(RED),A		; TURN RED OFF
	CALL	DELAAY
	LD	A,0FFH
	OUT	(GRN),A		; TURN GREEN ON
	CALL	DELAAY
	XOR	A
	OUT	(GRN),A		; TURN GREEN OFF
	CALL	DELAAY
	LD	A,0FFH
	OUT	(RED),A
	OUT	(GRN),A		; TURN YELLOW ON
	CALL	DELAAY
	XOR	A
	OUT	(RED),A
	OUT	(GRN),A		; TURN YELLOW OFF
	CALL	DELAAY
	CALL	BACKGND		; TEST ALL SOUNDS
	CALL	DELAAY
	CALL	MISS
	CALL	DELAAY
	CALL	GOODHT
	CALL	DELAAY
	CALL	HITOWN
	CALL	DELAAY
	CALL	IMHIT
	CALL	DELAAY
	JP	BEGIN
	EJEC

TDISP:
	OUT	(THOUS),A
	OUT	(HUNDS),A
	OUT	(TENS),A
	OUT	(UNITS),A
	OUT	(VAL10),A
	OUT	(VAL1),A
	OUT	(CRED),A
	RET

		; IR RESET

IRRST:
	LD	A,(IO)
	AND	0DFH
	LD	(CONT),A
	LD	A,(IO)
	LD	(CONT),A
	RET

		; GAME SOUNDS

BACKGND:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	BC,380H
	LD	DE,90H
	LD	HL,800H
	JP	SOUND

GOODHT:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	BC,180H
	LD	DE,60H
	LD	HL,9C02H
	CALL	SETSND
	POP	HL
	POP	DE
	LD	BC,600H
DEL25:
	DEC	C
	LD	A,C
	OR	A
	JP	NZ,DEL25
	DEC	B
	LD	A,B
	OR	A
	JP	NZ,DEL25
	POP	BC
	XOR	A
	OUT	(PTBS),A
	POP	AF
	RET

MISS:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	BC,200H
	LD	DE,40H
	LD	HL,0CC00H
	JP	SOUND

HITOWN:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	BC,180H
	LD	DE,90H
	LD	HL,0EC00H
	JP	SOUND


	IMHIT:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	BC,110H
	LD	DE,90H
	LD	HL,0AA00H
	JP	SOUND

SOUND:
	CALL	SETSND
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

		; GAME SOUND SUPPORT SUBROUTINE

SETSND:
	OUT	(STPT0S),A
	OUT	(STPT1S),A
	LD	A,03H
	OUT	(PTBS),A
	XOR	A
	OUT	(PTAS),A
	LD	A,C
	OUT	(T0LSBS),A
	LD	A,B
	OUT	(T0MSBS),A
	LD	A,E
	OUT	(T1LSBS),A
	LD	A,D
	OUT	(T1MSBS),A
	LD	A,H
	OUT	(PTAS),A
	OUT	(STRT0S),A
	OUT	(STRT1S),A
	LD	A,L
	OUT	(PTBS),A
	RET

		; DELAYS

DELAAY:
	PUSH	BC
	PUSH	AF
	LD	BC,0FFFFH
	JP	DELAY

DEL1K:
	PUSH	BC
	PUSH	AF
	LD	BC,9D82H
	JP	DELAY

DELAY:
	XOR	A
DECLO:
	CP	C
	JP	Z,DECHI
	DEC	BC
	JP	DECLO
DECHI:
	CP	B
	JP	Z,POPOP
	DEC	BC
	JP	DECLO
POPOP:
	POP	AF
	POP	BC
	RET

		; SEARCH FOR VALID CODES

SEARCH:
	LD	B,(HL)		; TBL LENGTH IN B
TS1:
	INC	HL
	CP	(HL)
	RET	Z		; ELEMENT FOUND
	DJNZ	TS1
	SCF
	RET			; ELEMENT NOT FOUND, CF SET

CODETBL:
	DB	40		; SHOULD IT BE 40H ????
	DB	42H,44H,46H,48H,4AH,4CH,4EH,50H,52H,54H
	DB	94H,82H,84H,86H,88H,8AH,8CH,8EH,90H,92H		'4-2-87
	DB	43H,45H,47H,49H,4BH,4DH,4FH,51H,53H,55H
	DB	81H,83H,85H,87H,89H,8BH,8DH,8FH,91H,93H

	END

