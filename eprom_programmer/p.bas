10 DEF SEG=0 :MEMCNT=4096 :DEFDBL S
20 KEY OFF
30 GOSUB 1550:REM PRINT SIGN ON
40 GOSUB 2870:REM INITIALIZE PORT
50 LOCATE 5,32:COLOR 11:PRINT"       MENU":COLOR 5
80 LOCATE 8,27
90 PRINT "1. CHECK FOR BLANK EPROM"
100 LOCATE 9,27
110 PRINT "2. PERFORM CHECKSUM ON EPROM"
120 LOCATE 10,27
237 PRINT "3. PROGRAM STANDARD EPROM"
238 LOCATE 11,27
239 PRINT "4. RETURN TO MAIN MENU"
240 LOCATE 13,29
250 PRINT "ENTER SELECTION ? ";
251 COLOR 19:PRINT CHR$(219):COLOR 5
255 A$=INKEY$:IF A$="" THEN 255
270 IF A$="1" THEN 1110
280 IF A$="2" THEN 960
290 IF A$="3" THEN 370
350 IF A$="4" THEN CLS:PRINT"***RETURNING TO MAIN MENU***":END
    IF A$=CHR$(27) THEN CLS:PRINT"***RETURNING TO MAIN MENU***":END
360 GOTO 240
365 REM -SUB XX PROM SELECTION MENU
370 GOSUB 1550
380 COLOR 11:LOCATE 6,26:PRINT"   PROGRAM STANDARD ROM'S   ":COLOR 5
390 LOCATE 8,20
420 PRINT "1. POD SOUND ROM #1.....P/N 595-0400-0001"
430 LOCATE 9,20
440 PRINT "2. POD SOUND ROM #2.....P/N 595-0400-0002"
450 LOCATE 10,20
500 PRINT "3. PHASOR STATION ROM...P/N 595-0700-0001"
510 LOCATE 11,20
520 PRINT "4. POD SIGNATURE ROM....P/N 595-1000-00XX"
530 LOCATE 12,20
540 PRINT "5. SOUND TEST ROM.......P/N 595-0007-0001"
550 LOCATE 13,20
560 PRINT "6. RF TEST STATION ROM..P/N 595-0011-0001"
590 LOCATE 15,30
600 PRINT "ENTER SELECTION (ESC TO RETURN TO ROM UTILITY MENU): ";
601 COLOR 19:PRINT CHR$(219):COLOR 5
602 A$=INKEY$:IF A$="" THEN 602
610 IF A$="1" THEN GOSUB 5900:GOTO 710
620 IF A$="2" THEN GOSUB 6000:GOTO 710
630 IF A$="3" THEN GOSUB 6300:GOTO 710
640 IF A$="4" THEN GOSUB 6400:GOTO 5000
650 IF A$="5" THEN GOSUB 6500:GOTO 710
660 IF A$="6" THEN GOSUB 6800:GOTO 710
705 IF A$=CHR$(27) THEN 10
708 GOTO 602
709 REM  USE PROM MENU PARAMETERS TO LOAD DATA FROM DISK **********************
710 GOSUB 720:GOTO 2990
720 GOSUB 1550
    GOSUB 2870
750 COLOR 11:LOCATE 6,18:PRINT"PROGRAM:";B$;"  P/N:";E$:COLOR 5
751 DEF SEG=&H2000:LENGTH=4096
752 LOCATE 8,28:COLOR 19:PRINT" LOADING DATA FROM DISK"
753 BLOAD "B:"+F$+".ROM",0  'changed to designate drive b: MPL 1-28-87
754 COLOR 5
755 RETURN
840 REM - SUB *** CALCULATE CHECKSUM FOR EPROM ********************************
960 GOSUB 1550
970 COLOR 11:LOCATE 6,28:PRINT"PERFORM CHECKSUM ON EPROM":COLOR 5
980 LOCATE 8,20
990 PRINT "INSERT EPROM INTO SOCKET AND PRESS RETURN"
992 LOCATE 9,20:PRINT "PRESS ESC TO RETURN TO THE ROM UTILITY MENU"
995 A$=INKEY$:IF A$=CHR$(27) THEN 10
996 IF A$=CHR$(13) THEN 1000 ELSE 995
1000 LOCATE 10,27
1010 COLOR 19
1020 PRINT" READING CONTENTS OF EPROM"
1030 COLOR 5
1040 GOSUB 2870
     S=0:A=0:B=0:CEC=0
     FOR Z=1 TO MEMCNT
        OUT &H3BE,&HC:'SET HIGH FOR LS NIBBLE
        A=INP(&H3BD) AND &HF0
        OUT &H3BE,&H4:'SET LOW FOR MS NIBBLE
        B=INP(&H3BD) AND &HF0
        CEC=A/16+B
	S=S+CEC
        OUT &H3BE,&HE:'INCREMENT ADDRESS
     NEXT Z
1050 LOCATE 10,24
1055 IF S>65535 THEN S=S-65536:GOTO 1055
1060 PRINT" CHECKSUM=       ";HEX$(S);" HEX        "
1070 LOCATE 12,18
1080 PRINT "DO YOU WANT TO PERFORM ANOTHER CHECKSUM (Y/N)";
1081 LOCATE 12,63:COLOR 19:PRINT CHR$(219):COLOR 5
1085 A$=INKEY$:IF A$="" THEN 1085
1086 IF A$="y" OR A$="Y" THEN 960
1087 IF A$="N" OR A$="n" THEN 10
1088 IF A$=CHR$(27) THEN 10
1090 GOTO 1085
1100 REM - SUB *** PRINTS SCREEN HEADINGS AND CALLS THE SUB TO CHECK FOR A 
     REM           BLANK PROM ***
1110 GOSUB 1550
1120 GOSUB 2870
1130 COLOR 11:LOCATE 6,30:PRINT"CHECK FOR BLANK EPROM":COLOR 5
1140 LOCATE 8,20
1150 PRINT "INSERT EPROM INTO SOCKET AND PRESS RETURN"
     LOCATE 9,20:PRINT "PRESS ESC TO RETURN TO THE ROM UTILITY MENU"
1151 A$=INKEY$:IF A$=CHR$(27) THEN 10
1152 IF A$=CHR$(13) THEN 1160 ELSE 1151
1160 LOCATE 10,27
1170 COLOR 19
1180 PRINT" READING CONTENTS OF EPROM"
1190 COLOR 5
1200 GOSUB 1750
1210 LOCATE 10,24
1220 IF BAD=0 THEN PRINT"       THE EPROM IS BLANK        "
1230 IF BAD<>0 THEN PRINT" THERE WERE";BAD;"BYTES PROGRAMMED"
1240 LOCATE 12,20
1250 PRINT "DO YOU WANT TO CHECK ANOTHER EPROM (Y/N)"
1255 LOCATE 12,60:COLOR 19:PRINT CHR$(219):COLOR 5
1260 A$=INKEY$:IF A$="Y" OR A$="y" THEN 1110
1265 IF A$="n" OR A$="N" THEN 10
1266 IF A$=CHR$(27) THEN 10 ELSE 1260
1300 REM - LINES 1500 - 1540 HAVE NO KNOWN USE ********************************
1500 GOSUB 1550
1510 COLOR 11
1520 LOCATE 8,25:PRINT Y$
1530 COLOR 5
1540 GOTO 840
1545 REM - SUBROUTINE - PRINT SCREEN HEADING **********************************
1550 CLS
1560 LOCATE 1,6
1570 PRINT CHR$(201);
1580 FOR X=7 TO 74
1590 PRINT CHR$(205);
1600 NEXT X
1610 PRINT CHR$(187);
1620 LOCATE 2,6
1630 PRINT CHR$(186);
1640 LOCATE 2,25
1650 PRINT"PHOTON PROM-PROGRAMMING UTILITY": 'SCREEN HEADING IS ONLY TEXT LINE
1660 LOCATE 2,75
1670 PRINT CHR$(186);
1680 LOCATE 3,6
1690 PRINT CHR$(200);
1700 FOR X=7 TO 74
1710 PRINT CHR$(205);
1720 NEXT X
1730 PRINT CHR$(188);
1740 RETURN
1745 REM - SUB *** CHECK FOR BLANK EPROM **************************************
1750 BAD=0:POINTER=0
1760 DEF SEG=&H2000:GOSUB 2870
1770 FOR X=1 TO MEMCNT
1775 OUT &H3BE,&HC:'SET HIGH FOR LS NIBBLE
1780 A=INP(&H3BD) AND &HF0
1790 OUT &H3BE,&H4:'SET LOW FOR MS NIBBLE
1800 B=INP(&H3BD) AND &HF0
1810 A=A/16+B:IF A<>&HFF THEN BAD=BAD+1
1830 OUT &H3BE,&HE:'INCREMENT ADDRESS
1840 NEXT X
1850 RETURN
1855 REM - SUB  *** READ EPROM INTO RAM ***
1860 GOSUB 1550:GOSUB 2870: GOSUB 6850
1870 COLOR 11:LOCATE 6,31:PRINT"LOAD RAM FROM EPROM":COLOR 5
1880 LOCATE 8,20
1890 PRINT "INSERT EPROM INTO SOCKET AND PRESS RETURN"
     LOCATE 9,20:PRINT "PRESS ESC TO RETURN TO ROM UTILITY MENU"
1892 A$=INKEY$:IF A$=CHR$(13) THEN 1900
1893 IF A$=CHR$(27) THEN 10 ELSE 1892
1900 LOCATE 10,27
1910 COLOR 19
1920 PRINT" READING CONTENTS OF EPROM"
1930 COLOR 5
1940 POINTER=0:DEF SEG=&H2000
1950 FOR X=1 TO MEMCNT:' READ EPROM INTO RAM
1955 OUT &H3BE,&HC:'SET HIGH FOR LS NIBBLE
1960 A=INP(&H3BD) AND &HF0
1970 OUT &H3BE,&H4:'SET LOW FOR MS NIBBLE
1980 B=INP(&H3BD) AND &HF0
1990 A=A/16+B:POKE POINTER,A:POINTER=POINTER+1
2010 OUT &H3BE,&HE:'INCREMENT ADDRESS
2020 NEXT X
2030 LOCATE 10,22
2040 PRINT "          READ COMPLETED         "
2050 LOCATE 12,19
2060 PRINT "DO YOU WANT TO READ ANOTHER EPROM (Y/N)"
2062 LOCATE 12,58:COLOR 19:PRINT CHR$(219):COLOR 5
2065 A$=INKEY$:IF A$=CHR$(27) THEN 10
2070 IF A$="Y" OR A$="y" THEN 1860
2080 IF A$="N" OR A$="n" THEN 10
2085 GOTO 2065
2086 REM - SUB *** DISPLAY RAM CONTENTS. THERE IS NO EDIT CAPABILITY.**********
2090 GOSUB 2870:GOSUB 1550
2100 COLOR 11:LOCATE 4,32:PRINT"DISPLAY RAM ":COLOR 5
2110 REM
2120 POINTER=0:DEF SEG=&H2000
2130 Q=0:Z=0:Y=15
2140 LOCATE 5,1
2150 PRINT "     Addr  -0 -1 -2 -3 -4 -5 -6 -7 -8 -9 -A -B -C -D -E -F ---IBM ASCII----"
2160 FOR N=Z TO Y
2170 IF N<255 THEN PRINT "     0";
2180 IF Q<16 THEN 2400
2190 PRINT HEX$(Q);"X  ";
2200 GOSUB 2260
2210 PRINT
2220 Q=Q+1
2230 NEXT N
2240 GOTO 2440
2250 REM - SUBROUTINE - DISPLAY RAM CONTENTS **********************************
2260 FOR X=0 TO 15
2270 A=PEEK(POINTER):POINTER=POINTER+1
2280 IF A<16 THEN PRINT "0";
2290 PRINT HEX$(A);" ";
2300 NEXT X
2310 POINTER=POINTER-16
2320 FOR T=0 TO 15
2330 S=PEEK(POINTER)
2340 IF S<32 THEN 2420
2350 IF S=255 THEN 2420
2360 PRINT CHR$(S);
2370 POINTER=POINTER+1
2380 NEXT T
2390 RETURN
2400 PRINT "0";HEX$(N);"X  ";
2410 GOTO 2200
2420 PRINT ".";
2430 GOTO 2370
2440 PRINT
2450 LOCATE 23,6
2460 PRINT "(N)NEXT BLOCK (E)EXIT ? ";
2465 COLOR 19:PRINT CHR$(219);
2466 COLOR 5
2468 A$=INKEY$:IF A$="" THEN 2468
2470 IF A$="N" OR A$="n" THEN LOCATE 23,30:PRINT A$:Y=Z+15:GOTO 2140
2480 IF A$="E" OR A$="e" THEN LOCATE 23,30:PRINT A$:GOTO 10
2485 IF A$=CHR$(27) THEN 10
2500 GOTO 2468
2510 REM - SUB - SAVE RAM CONTENTS TO DISC FILE *******************************
2560 GOSUB 1550:GOSUB 2870: GOSUB 6850
2570 COLOR 11:LOCATE 6,30:PRINT"SAVE RAM TO DISK FILE":COLOR 5
2580 LENGTH=4096:DEF SEG=&H2000
2590 LOCATE 8,16
2600 PRINT"ENTER FILENAME FOR SAVE, THEN PRESS RETURN   ________"
2610 LOCATE 8,59:INPUT Y$
2620 V=LEN(Y$):IF V>8 THEN 2700
2630 Y$=Y$+".ROM"
2640 PRINT
2650 BSAVE Y$,0,LENGTH
2660 LOCATE 10,19
2670 PRINT "SAVE COMPLETED, RETURN TO ROM UTILITY MENU (Y/N) "
2675 LOCATE 10,60:COLOR 19:PRINT CHR$(219):COLOR 5
2680 A$=INKEY$:IF A$="" THEN 2680
2682 IF A$="N" OR A$="n" THEN 2560 ELSE 10
2700 LOCATE 8,48:PRINT " ________                     "
2710 GOTO 2590
2715 REM - SUB - LOAD RAM FROM DISC FILE **************************************
2720 GOSUB 1550:GOSUB 2870: GOSUB 6850
2730 COLOR 11:LOCATE 6,29:PRINT"LOAD RAM FROM DISK FILE":COLOR 5
2740 DEF SEG=&H2000
2750 LOCATE 8,16
2760 PRINT"ENTER FILENAME FOR LOAD, THEN PRESS RETURN   ________"
2770 LOCATE 8,59:INPUT Y$
2780 V=LEN(Y$):IF V>8 THEN 2850
2790 Y$=Y$+".ROM"
2800 BLOAD Y$,0
2810 LOCATE 10,20
2820 PRINT "LOAD COMPLETED, RETURN TO ROM UTILITY MENU (Y/N)"
2825 LOCATE 10,61:COLOR 19:PRINT CHR$(219):COLOR 5
2830 A$=INKEY$:IF A$="" THEN 2830
2840 IF A$="N" OR A$="n" THEN 2720 ELSE 10
2850 LOCATE 8,48:PRINT " ________            "
2860 GOTO 2770
2870 REM - SUB - INITIALIZE I/O PORTS *****************************************
2880 REM THIS COMMAND SETS(PGM/PULSE=1)(RD/WR=1)(ADDR/PULSE=1)(HI/LOW=0)
2890 OUT &H3BE,&HC
2900 REM THIS COMMAND RESETS THE ADDRESS COUNTER
2910 OUT &H3BE,&HD:OUT &H3BE,&HC
2920 REM THIS COMMAND SETS THE DATA OUTPUT PORT TO ALL 0'S
2930 OUT &H3BC,&H0
2940 RETURN
2950 REM - SUB - INITIALIZE I/O PORTS *****************************************
2960 GOSUB 1550
2970 COLOR 11:LOCATE 6,26:PRINT"   PROGRAM EPROM FROM RAM  ":COLOR 5
2980 GOSUB 2870
2990 LOCATE 8,16
3000 PRINT "INSERT BLANK EPROM INTO SOCKET AND CONNECT CURRENT"
     LOCATE 9,16
     PRINT "METER TO PROGRAMMER, THEN PRESS RETURN"
     LOCATE 10,20:PRINT "PRESS ESC TO RETURN TO ROM UTILITY MENU"
     LOCATE 20,10
     PRINT "NOTE: CURRENT METER READING SHOULD NEVER EXCEED 50 mA."
     LOCATE 21,10
     PRINT "REMOVE ROM IMMEDIATELY IF THIS SPEC IS EXCEEDED. ROM IS BAD."
3005 A$=INKEY$:IF A$=CHR$(27) THEN 10
3006 IF A$<>CHR$(13) THEN 3005
3010 LOCATE 11,17
3020 COLOR 19:PRINT"                 PROGRAMMING"
3030 REM THIS COMMAND ENTERS THE WRITE MODE
3040 OUT &H3BE,&H8
3050 FOR Z=1 TO 1000:NEXT Z:REM WAIT .6 SECOND
3060 DEF SEG=&H2000
3070 POINTER=0
3080 FOR X=1 TO MEMCNT
3090 A=PEEK(POINTER):OUT &H3BC,A
3100 GOSUB 3220:REM PROGRAM PULSE
3110 OUT &H3BE,&HA:OUT &H3BE,&H8:REM INCREMENT ADDRESS
3120 POINTER=POINTER+1
3130 NEXT X
3140 GOSUB 2870:REM ENTER READ MODE
3145 BEEP
	LOCATE 11,1:GOSUB 5570

3150 LOCATE 11,18
3160 COLOR 19:PRINT"                  VERIFYING  "
3170 GOSUB 3280
	LOCATE 10,1:GOSUB 5570

3180 LOCATE 10,24
3190 COLOR 5:PRINT"THERE WERE";BAD;"VERIFICATION ERRORS"
3200 LOCATE 12,27:PRINT "PROGRAM ANOTHER EPROM (Y/N)"
3210 LOCATE 12,54:COLOR 19:PRINT CHR$(219):COLOR 5
3212 A$=INKEY$:IF A$="Y" OR A$="y" THEN 370
3215 IF A$="N" OR A$ ="n" THEN 10
3217 GOTO 3212
3220 REM - SUB -  *************** PROGRAM PULSE *************
3230 OUT &H3BE,&H9:REM SET PGM PULSE LOW
3240 FOR Y=1 TO 84:NEXT Y
3250 OUT &H3BE,&H8:REM SET PGM PULSE HIGH
3260 RETURN
3270 REM - SUB - COMPARES EPROM TO RAM ****************************************
3280 BAD=0:POINTER=0
3290 DEF SEG=&H2000
3300 FOR X=1 TO MEMCNT
3310 OUT &H3BE,&HC:'SET HIGH FOR L.S. NIBBLE
3320 A=INP(&H3BD) AND &HF0
3350 OUT &H3BE,&H4:'SET LOW FOR M.S. NIBBLE 
3330 B=INP(&H3BD) AND &HF0
3340 A=A/16+B:IF A<>PEEK(POINTER) THEN BAD=BAD+1
3360 OUT &H3BE,&HE:'INCREMENT ADDRESS
3370 POINTER=POINTER+1
3380 NEXT X
3390 RETURN
3394 REM - SUB - COMPARE EPROM TO RAM *****************************************
4000 GOSUB 1550:GOSUB 2870: GOSUB 6850
4010 COLOR 11:LOCATE 6,31:PRINT"VERIFY RAM TO EPROM":COLOR 5
4020 LOCATE 8,20
4030 PRINT "INSERT EPROM INTO SOCKET AND PRESS RETURN"
     LOCATE 9,20:PRINT "PRESS ESC TO RETURN TO ROM UTILITY MENU"
4032 A$=INKEY$:IF A$=CHR$(13) THEN 4040
4033 IF A$=CHR$(27) THEN 10 ELSE 4032
4040 LOCATE 10,18
4050 COLOR 19:PRINT"                  VERIFYING  "
4060 GOSUB 3280
4070 LOCATE 10,25
4080 COLOR 5:PRINT"THERE WERE";BAD;"VERIFICATION ERRORS"
4090 LOCATE 12,27:PRINT "VERIFY ANOTHER EPROM (Y/N)"
4095 LOCATE 12,53:COLOR 19:PRINT CHR$(219):COLOR 5
4100 A$=INKEY$:IF A$="Y" OR A$="y" THEN 4000
4110 IF A$="N" OR A$="n" THEN 10
4120 IF A$=CHR$(27) THEN 10 ELSE 4100
5000 GOSUB 720
     LOCATE 6,1:GOSUB 5570
     LOCATE 8,1:GOSUB 5570
5080 LOCATE 5,20
5090 INPUT "ENTER POD SIGNATURE # (1-33): ",B    '12-9-86

	'----GET SIGNATURE  12-9-86 MPL
	GOSUB 20000
	IF GETFLAG=1 THEN BEEP:GOTO 5080
	LOCATE 5,1: GOSUB 5570
	COLOR 11:LOCATE 5,25:PRINT"PROGRAMMING: P/N 595-1000-00";
	B$=STR$(B)
	IF LEN(B$) < 3 THEN PRINT "0"+B$ ELSE PRINT B$
	COLOR 5

5221 POKE &H0FFF,C	'12-9-86
     GOTO 2990

     REM - SUB - PRINT ONE BLANK LINE
5570 FOR G=0 TO 79
5580 PRINT " ";
5590 NEXT G
5600 RETURN
5610 REM - SUB - READ A BYTE, DONT INCREMENT ADDRESS **************************
5630 OUT &H3BE,&HC
5620 R=INP(&H3BD) AND &HF0
5660 OUT &H3BE,&H4
5640 D=INP(&H3BD) AND &HF0
5650 Q=R/16+D
5670 RETURN:REM - THESE SUB ROUTINES SET PARAMETERS FOR PROM SELECTION MENU **
5800 B$="POD SIGNATURE ROM "
5820 E$="595-0800-0000"
5830 F$="PODGAME"
     MEMCNT=4096 
5840 RETURN
5900 B$=" POD SOUND ROM #1 "
5910 E$="595-0400-0001"
5930 F$="SOUND1"
     MEMCNT=4096
5940 RETURN
6000 B$=" POD SOUND ROM #2 "
6020 E$="595-0400-0002"
6030 F$="SOUND2"
     MEMCNT=4096
6040 RETURN
6100 B$=" SLAVE BOARD ROM  "
6120 E$="595-0600-0001"
6130 F$="SLAVE"
     MEMCNT=4096
6140 RETURN
6200 B$="ENTRY TERMINAL ROM"
6220 E$="595-0300-0001"
6230 F$="ET"
     MEMCNT=4096
6240 RETURN
6300 B$="PHASOR STATION ROM"
6320 E$="595-0700-0001"
6330 F$="PHASER"
     MEMCNT=4096
6340 RETURN
6400 B$="POD SIGNATURE ROM"
6420 E$="595-1000-00XX"
6430 F$="PODGAME"
     MEMCNT=4096
6440 RETURN
6500 B$="  SOUND TEST ROM  "
6520 E$="595-0007-0001"
6530 F$="TESTDEMO"
     MEMCNT=4096
6540 RETURN
6600 B$="CEILING RADIO ROM "
6620 E$="595-0010-0001"
6630 F$="CELRADIO"
     MEMCNT=4096
6640 RETURN
6700 B$=" TEST TARGET ROM  "
6720 E$="595-0005-0001"
6730 F$="TESTTARG"
     MEMCNT=4096
6740 RETURN
6800 B$=" RF TEST STATION ROM  "
6810 E$="595-0011-0001"
6820 F$="ANSRBACK"
     MEMCNT=4096
     RETURN
6850 '  SUBROUTINE FOR INITIALIZING KNOWN VARIABLES
	'
     A=0: B=0
     RETURN


'----GET SIGNATURE   12-9-86

20000	GETFLAG =0

	IF B=1 THEN C=&H60:RETURN
	IF B=2 THEN C=&H61:RETURN
	IF B=3 THEN C=&H62:RETURN 
	IF B=4 THEN C=&H63:RETURN
	IF B=5 THEN C=&H64:RETURN
	IF B=6 THEN C=&H65:RETURN
	IF B=7 THEN C=&H66:RETURN
	IF B=8 THEN C=&H67:RETURN
	IF B=9 THEN C=&H68:RETURN
	IF B=10 THEN C=&H69:RETURN
	IF B=11 THEN C=&H70:RETURN
	IF B=12 THEN C=&H71:RETURN
	IF B=13 THEN C=&H72:RETURN
	IF B=14 THEN C=&H73:RETURN
	IF B=15 THEN C=&H74:RETURN
	IF B=16 THEN C=&H75:RETURN
	IF B=17 THEN C=&H76:RETURN
	IF B=18 THEN C=&H77:RETURN
	IF B=19 THEN C=&H78:RETURN
	IF B=20 THEN C=&H79:RETURN
	IF B=21 THEN C=&H6A:RETURN
	IF B=22 THEN C=&H6B:RETURN
	IF B=23 THEN C=&H6C:RETURN
	IF B=24 THEN C=&H6D:RETURN
	IF B=25 THEN C=&H6E:RETURN
	IF B=26 THEN C=&H6F:RETURN
	IF B=27 THEN C=&H7A:RETURN
	IF B=28 THEN C=&H7B:RETURN
	IF B=29 THEN C=&H7C:RETURN
	IF B=30 THEN C=&H7D:RETURN
	IF B=31 THEN C=&H7E:RETURN
	IF B=32 THEN C=&H7F:RETURN
	IF B=33 THEN C=&H5F:RETURN

	GETFLAG=1

        RETURN

